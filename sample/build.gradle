plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.hilt)
    alias(libs.plugins.firebase.appdistribution)
}

android {
    namespace 'com.spendesk.grapes.samples'

    compileSdkVersion libs.versions.androidCompileSdk.get() as Integer

    defaultConfig {
        applicationId "com.spendesk.grapes.samples"
        minSdkVersion libs.versions.androidMinSdk.get() as Integer
        targetSdkVersion libs.versions.androidTargetSdk.get() as Integer
        versionCode 1
        versionName libs.versions.grapes.version.get()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.kotlin.compiler.get()
    }

    signingConfigs {
        release {
            storeFile file("spendesk-grapes.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }

        debug {
            storeFile file('debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                groups = "release-candidate"
                appId = "1:1044115290075:android:0ab502faa6ab13f24b59fe"
            }
        }

        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
    }

    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(path: ':library')
    implementation project(path: ':library-compose')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.20"
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.fragment.ktx)

    implementation(libs.material)

    // JUNIT
    testImplementation(libs.junit4)
    androidTestImplementation(libs.junit.android)
    androidTestImplementation(libs.espresso.core)

    // RX
    implementation(libs.rxJava)
    implementation(libs.rxJava.android)
    implementation(libs.rxBinding)

    // HILT
    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)

    // REFLECTION
    implementation(libs.reflection)
}
